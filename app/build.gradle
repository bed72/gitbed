plugins {
    id 'kotlin-android'
    id 'com.android.application'
    id "com.google.protobuf" version "0.8.12"
}

apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs'

def apikeyProperties = new Properties()
def apikeyPropertiesFile = rootProject.file("apikey.properties")
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.bed.gitbed"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders['pathName'] = '/auth'
        manifestPlaceholders['schemeName'] = 'gitbed'
        manifestPlaceholders['hostName'] = 'open.bed72.gitbed'
        manifestPlaceholders['pathHost'] = 'www.open.bed72.gitbed'

        buildConfigField 'String', 'DATA_STORE', '"gitbed.pb"'
        buildConfigField 'String', 'BASE_URL', '"https://api.github.com/"'
        buildConfigField 'String', 'REDIRECT_URL', '"gitbed://open.bed72.gitbed/auth"'
        buildConfigField 'String', 'AUTHORIZATION_URL', '"https://github.com/login/oauth/authorize"'
        buildConfigField 'String', 'AUTHENTICATION_URL', '"https://github.com/login/oauth/access_token"'

        buildConfigField('String', 'GITHUB_ID', apikeyProperties['GITHUB_ID'])
        buildConfigField('String', 'GITHUB_SECRET', apikeyProperties['GITHUB_SECRET'])
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn']
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // AndroidX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.work:work-runtime:2.7.1' // -> sdk 12
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // Material design
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.5.0'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Koin
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-navigation:$koin_version"
    implementation "io.insert-koin:koin-androidx-workmanager:$koin_version"

    // Paging3
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Navigation
    def nav_version = '2.4.0-rc01'
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"

    // ViewModel and LiveData
    def lifecycle_version = '2.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Glide
    def glide_version = '4.12.0'
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // DataStore
    def datastore_version = '1.0.0'
    implementation "androidx.datastore:datastore-core:${datastore_version}"
    implementation "androidx.datastore:datastore-preferences:${datastore_version}"

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp
    def okhttp_version = '5.0.0-alpha.3'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version")

    // Other Libs
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.protobuf:protobuf-javalite:3.14.0'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Memory Leak
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.14.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}